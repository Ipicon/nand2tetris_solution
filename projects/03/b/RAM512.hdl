// This file is part of the materials accompanying the book 
// "The Elements of Computing Systems" by Nisan and Schocken, 
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/b/RAM512.hdl

/**
 * Memory of 512 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then 
 * the in value is loaded into the memory location specified by address 
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM512 {
    IN in[16], load, address[9];
    OUT out[16];

    PARTS:
    DMux8Way(in=load, sel=address[6..8], a=regA, b=regB, c=regC, d=regD, e=regE, f=regF, g=regG, h=regH);
    RAM64(in=in, load=regA, address=address[0..5], out=outA);
    RAM64(in=in, load=regB, address=address[0..5], out=outB);
    RAM64(in=in, load=regC, address=address[0..5], out=outC);
    RAM64(in=in, load=regD, address=address[0..5], out=outD);
    RAM64(in=in, load=regE, address=address[0..5], out=outE);
    RAM64(in=in, load=regF, address=address[0..5], out=outF);
    RAM64(in=in, load=regG, address=address[0..5], out=outG);
    RAM64(in=in, load=regH, address=address[0..5], out=outH);
    Mux8Way16(a=outA, b=outB, c=outC, d=outD, e=outE, f=outF, g=outG, h=outH, sel=address[6..8], out=out);
}